<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- $This file is distributed under the terms of the license in /doc/license.txt$ -->

<!-- VIVO-specific default list view config file for object properties 
    
     See guidelines in vitro/doc/list_view_configuration_guidelines.txt -->

<list-view-config>
    <query-select>    
        PREFIX afn:  &lt;http://jena.hpl.hp.com/ARQ/function#&gt;
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX foaf: &lt;http://xmlns.com/foaf/0.1/&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;  
        PREFIX vitro: &lt;http://vitro.mannlib.cornell.edu/ns/vitro/0.7#&gt;
          
        SELECT <collated> ?subclass </collated>
               ?object 
               ?label 
               ?localName 
               ?type
               ?title 
               ?dateTimeStart ?dateTimeEnd WHERE {
  
            ?subject ?property ?object 
                LET (?localName := afn:localname(?object)) 
            
            OPTIONAL { ?object rdfs:label ?label } 

            OPTIONAL {
                # Get mostSpecificType only for Persons
                ?object a foaf:Person .
                ?object vitro:mostSpecificType ?typeUri .
                ?typeUri rdfs:label ?type .
                # Display only a mostSpecificType that belongs to a classgroup.
                ?typeUri vitro:inClassGroup ?classGroup .
                ?classGroup a vitro:ClassGroup 
            }
            
            OPTIONAL { ?object core:preferredTitle ?title }
            OPTIONAL { ?object core:dateTimeInterval ?dateTimeInterval 
            OPTIONAL { ?dateTimeInterval core:start ?dateTimeStartValue .
                      ?dateTimeStartValue core:dateTime ?dateTimeStart 
            }
            OPTIONAL { ?dateTimeInterval core:end ?dateTimeEndValue .
                      ?dateTimeEndValue core:dateTime ?dateTimeEnd 
            }
            } 
            
            <collated>
            OPTIONAL { 
                ?object a ?subclass .
                ?subclass vitro:inClassGroup ?classgroup . } 
            FILTER ( afn:namespace(?subclass) != "http://vitro.mannlib.cornell.edu/ns/vitro/0.7#" )   
            </collated>
        
        # Order by ?type is important, because if the object has more than one mostSpecificType,
        # they all get returned, but postprocessing culls out all but one. Ordering by ?type
        # will ensure that at least we'll get the same one every time.
        } ORDER BY <collated> ?subclass </collated> 
        DESC(?dateTimeEnd) DESC(?dateTimeStart) 
        ASC( ?label ) ASC( ?localName ) ?type 
    </query-select>

    <query-construct>
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;  
        PREFIX vitro: &lt;http://vitro.mannlib.cornell.edu/ns/vitro/0.7#&gt;
        
        CONSTRUCT {
            ?subject ?property ?object .
            ?object a ?subclass .
            ?object rdfs:label ?label .
        } WHERE {
            { 
                ?subject ?property ?object .
            } UNION {                
                ?subject ?property ?object .
                ?object a ?subclass . 
            } UNION {      
                ?subject ?property ?object .
                ?object rdfs:label ?label .
            }  
        }
    </query-construct>
    
    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;  
        PREFIX vitro: &lt;http://vitro.mannlib.cornell.edu/ns/vitro/0.7#&gt;

        CONSTRUCT {
            ?subject ?property ?object .
            ?object vitro:mostSpecificType ?typeUri .
            ?typeUri vitro:inClassGroup ?classGroup .
            ?classGroup a vitro:ClassGroup .
            ?typeUri rdfs:label ?type .
            ?object core:preferredTitle ?title .
        } WHERE {
            { 
                ?subject ?property ?object .
                ?object vitro:mostSpecificType ?typeUri .
                ?typeUri rdfs:label ?type .
                ?typeUri vitro:inClassGroup ?classGroup .
                ?classGroup a vitro:ClassGroup 
            } UNION {            
                ?subject ?property ?object .
                ?object core:preferredTitle ?title .
            }          
        }
    </query-construct>

    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;   
        CONSTRUCT { 
            ?subject ?property ?object .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:start ?dateTimeStartValue .
            ?dateTimeStartValue core:dateTime ?dateTimeStart 
        } WHERE {
            ?subject ?property ?object .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:start ?dateTimeStartValue .
            ?dateTimeStartValue core:dateTime ?dateTimeStart 
        } 
    </query-construct>

    <query-construct>
        PREFIX core: &lt;http://vivoweb.org/ontology/core#&gt;   
        CONSTRUCT { 
            ?subject ?property ?object .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:end ?dateTimeEndValue .
            ?dateTimeEndValue core:dateTime ?dateTimeEnd 
        } WHERE {
            ?subject ?property ?object .  
            ?role core:dateTimeInterval ?dateTimeInterval .
            ?dateTimeInterval core:end ?dateTimeEndValue .
            ?dateTimeEndValue core:dateTime ?dateTimeEnd 
        } 
    </query-construct>

    <template>propStatement-default.ftl</template>
</list-view-config>
