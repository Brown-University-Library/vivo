<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- $This file is distributed under the terms of the license in /doc/license.txt$ -->

<!-- See guidelines in vitro/doc/list_view_configuration_guidelines.txt -->

<!-- 
subject is CanonicalCourse, property is hasDepartmentOffering
example:
    'subject': '<http://vivo.brown.edu/individual/dept-42-subj-mcm-crs-1700s-narrative-and-immersion>',
    'property': '<http://vivo.brown.edu/ontology/vivo-brown/hasDepartmentOffering>'
-->

<list-view-config>
    <query-select>
        PREFIX blocal: &lt;http://vivo.brown.edu/ontology/vivo-brown/&gt;
        PREFIX vivo: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX owl: &lt;http://www.w3.org/2002/07/owl#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        
        SELECT DISTINCT ?faculty
                        ?facLabel
                        ?termList
                        
            WHERE {
                        
                ?subject ?property ?faculty .

                OPTIONAL {
                    ?faculty rdfs:label ?facLabel .
                }

                ?faculty blocal:fullTermList ?termList .

                <critical-data-required>
                FILTER ( bound(?faculty) )
                </critical-data-required>
        
            } ORDER BY ?facLabel
    </query-select>

    <query-construct>
        PREFIX vivo: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX blocal: &lt;http://vivo.brown.edu/ontology/vivo-brown/&gt;
        
        CONSTRUCT {
            ?subject ?property ?faculty .
            ?faculty rdfs:label ?facLabel .
        } WHERE {
            {
                ?subject ?property ?course .
                ?course vivo:realizedRole ?role .
                ?role vivo:teacherRoleOf ?faculty .
            } UNION {
                ?subject ?property ?course .
                ?course vivo:realizedRole ?role .
                ?role vivo:teacherRoleOf ?faculty .
                ?faculty rdfs:label ?facLabel .
            }
        }
    </query-construct>

    <query-construct>
        PREFIX vivo: &lt;http://vivoweb.org/ontology/core#&gt;
        PREFIX rdfs: &lt;http://www.w3.org/2000/01/rdf-schema#&gt;
        PREFIX blocal: &lt;http://vivo.brown.edu/ontology/vivo-brown/&gt;

        CONSTRUCT {?faculty blocal:fullTermList ?termList .}
        WHERE {
            SELECT ?faculty (GROUP_CONCAT(?termLabel; SEPARATOR = ", ") as ?termList)
            WHERE {
                SELECT DISTINCT ?faculty ?termLabel ?startValue
                WHERE {
                    ?subject ?property ?course .
                    ?course vivo:realizedRole ?role .
                    ?role vivo:teacherRoleOf ?faculty .
                    ?course vivo:dateTimeInterval ?dateTimeInterval .
                    ?dateTimeInterval vivo:start ?startDate .
                    ?dateTimeInterval rdfs:label ?termLabel .
                    ?startDate vivo:dateTime ?startValue

                } ORDER BY ?startValue

            } GROUP BY ?faculty
        }
    </query-construct>
    <template>propStatement-hasDepartmentOffering.ftl</template>
</list-view-config>
